PREFIX bdr-obsprops: <https://linked.data.gov.au/dataset/bdr/obsprops/>
PREFIX olis: <https://olis.dev/>
PREFIX schema: <https://schema.org/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX tern: <https://w3id.org/tern/ontologies/tern/>
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT ?rg_uri ?rg_label (SUM(?val) AS ?total) (NOW() as ?queryRunTime)
WHERE {
    GRAPH olis:SystemGraph {
        ?rg sosa:isFeatureOfInterestOf [
            sosa:observedProperty ?rg_uri ;
            sosa:hasResult [schema:value ?val]
        ].
        ?rg a olis:RealGraph .
    }

    # Define category URIs to labels mapping
    BIND(IF(?rg_uri = bdr-obsprops:DatasetsCount, "Datasets",
            IF(?rg_uri = bdr-obsprops:SamplesCount, "Samples",
                IF(?rg_uri = bdr-obsprops:SamplingsCount, "Samplings",
                    IF(?rg_uri = bdr-obsprops:ObservationsCount, "Observations",
                        "Unknown")))) AS ?rg_label
    )
    BIND(
        IF(?rg_uri = bdr-obsprops:DatasetsCount, 1,
            IF(?rg_uri =  bdr-obsprops:SamplesCount, 2,
                IF(?rg_uri = bdr-obsprops:SamplingsCount, 3,
                    IF(?rg_uri = bdr-obsprops:ObservationsCount, 4,
                        5)))) AS ?sortOrder
    )
}
GROUP BY ?rg_label ?rg_uri ?sortOrder
ORDER BY ?sortOrder